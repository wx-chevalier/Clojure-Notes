# 集合

Clojure 提供这些集合类型: list, vector, set, map。同时 Clojure 还可以使用 Java 里面提供的将所有的集合类型，但是通常不会这样做的， 因为 Clojure 自带的集合类型更适合函数式编程。

Clojure 集合有着 java 集合所不具备的一些特性。所有的 clojure 集合是不可修改的、异源的以及持久的。不可修改的意味着一旦一个集合产生之后，你不能从集合里面删除一个元素，也往集合里面添加一个元素。异源的意味着一个集合里面可以装进任何东西（而不必须要这些东西的类型一样）。持久的以为着当一个集合新的版本产生之后，旧的版本还是在的。CLojure 以一种非常高效的，共享内存的方式来实现这个的。比如有一个 map 里面有一千个 name-valuea pair, 现在要往 map 里面加一个，那么对于那些没有变化的元素， 新的 map 会共享旧的 map 的内存，而只需要添加一个新的元素所占用的内存。

有很多核心的函数可以用来操作所有这些类型的集合。。多得以至于无法在这里全部描述。其中的一小部分我们会在下面介绍 vector 的时候介绍一下。要记住的是，因为 clojure 里面的集合是不可修改的，所以也就没有对集合进行修改的函数。相反 clojure 里面提供了一些函数来从一个已有的集合来高效地创建新的集合 — 使用 [persistent data structures](http://en.wikipedia.org/wiki/Persistent_data_structure) 。同时也有一些函数操作一个已有的集合（比如 vector)来产生另外一种类型的集合(比如 LazySeq), 这些函数有不同的特性。

提醒： 这一节里面介绍的 Clojure 集合对于学习 clojure 来说是非常的重要。但是这里介绍一个函数接着一个函数，所以你如果觉得有点烦，有点乏味，你可以跳过，等用到的时候再回过头来查询。

`count` 返回集合里面的元素个数，比如：

```
(count [19 "yellow" true]) ; -> 3
```

`conj` 函数是 conjoin 的缩写, 添加一个元素到集合里面去，到底添加到什么位置那就取决于具体的集合了，我们会在下面介绍具体集合的时候再讲。

`reverse` 把集合里面的元素反转。

```
(reverse [2 4 7]) ; -> (7 4 2)
```

`map` 对一个给定的集合里面的每一个元素调用一个指定的方法，然后这些方法的所有返回值构成一个新的集合（LazySeq）返回。这个指定了函数也可以有多个参数，那么你就需要给 map 多个集合了。如果这些给的集合的个数不一样，那么执行这个函数的次数取决于个数最少的集合的长度。比如：

```
; The next line uses an anonymous function that adds 3 to its argument.
(map #(+ % 3) [2 4 7]) ; -> (5 7 10)
(map + [2 4 7] [5 6] [1 2 3 4]) ; adds corresponding items -> (8 12)
```

`apply` 把给定的集合里面的所有元素一次性地给指定的函数作为参数调用，然后返回这个函数的返回值。所以 apply 与 map 的区别就是 map 返回的还是一个集合，而 apply 返回的是一个元素， 可以把 apply 看作是 SQL 里面的聚合函数。比如：

```clj
(apply + [2 4 7]); -> 13
```

有很多函数从一个集合里面获取一个元素，比如：

```clj
(def stooges ["Moe" "Larry" "Curly" "Shemp"])
(first stooges) ; -> "Moe"
(second stooges) ; -> "Larry"
(last stooges) ; -> "Shemp"
(nth stooges 2) ; indexes start at 0 -> "Curly"
```

也有一些函数从一个集合里面获取多个元素，比如：

```clj
(next stooges) ; -> ("Larry" "Curly" "Shemp")
(butlast stooges) ; -> ("Moe" "Larry" "Curly")
(drop-last 2 stooges) ; -> ("Moe" "Larry")
; Get names containing more than three characters.
(filter #(> (count %) 3) stooges) ; -> ("Larry" "Curly" "Shemp")
(nthnext stooges 2) ; -> ("Curly" "Shemp")
```

有一些谓词函数测试集合里面每一个元素然后返回一个布尔值，这些函数都是”short-circuit”的，一旦它们的返回值能确定它们就不再继续测试剩下的元素了，有点像 java 的&&和 or, 比如:

```clj
(every? #(instance? String %) stooges) ; -> true
(not-every? #(instance? String %) stooges) ; -> false
(some #(instance? Number %) stooges) ; -> nil
(not-any? #(instance? Number %) stooges) ; -> true
```

# 列表

Lists 是一个有序的元素的集合 — 相当于 java 里面的 LinkedList。这种集合对于那种一直要往最前面加一个元素，干掉最前面一个元素是非常高效的(O(1)) — 想到于 java 里面的堆栈, 但是没有高效的方法来获取第 N 个元素， 也没有高效的办法来修改第 N 个元素。

下面是创建同样的 list 的多种不同的方法：

```
(def stooges (list "Moe" "Larry" "Curly"))
(def stooges (quote ("Moe" "Larry" "Curly")))
(def stooges '("Moe" "Larry" "Curly"))
```

`some` 可以用来检测一个集合是否含有某个元素. 它的参数包括一个谓词函数以及一个集合。你可以能会想了，为了要看一个 list 到底有没有某个元素为什么要指定一个谓词函数呢？其实我们是故意这么做来让你尽量不要这么用的。从一个 list 里面搜索一个元素是线性的操作（不高效），而要从一个 set 里面搜索一个元素就容易也高效多了，看下面的例子对比：

```
(some #(= % "Moe") stooges) ; -> true
(some #(= % "Mark") stooges) ; -> nil
; Another approach is to create a set from the list
; and then use the contains? function on the set as follows.
(contains? (set stooges) "Moe") ; -> true
```

`conj` 和 `cons` 函数的作用都是通过一个已有的集合来创建一个新的包含更多元素的集合 — 新加的元素在最前面。 `remove` 函数创建一个只包含所指定的谓词函数测试结果为 false 的元素的集合:

```
(def more-stooges (conj stooges "Shemp")) -> ("Shemp" "Moe" "Larry" "Curly")
(def less-stooges (remove #(= % "Curly") more-stooges)) ; -> ("Shemp" "Moe" "Larry")
```

`into` 函数把两个 list 里面的元素合并成一个新的大 list

```
(def kids-of-mike '("Greg" "Peter" "Bobby"))
(def kids-of-carol '("Marcia" "Jan" "Cindy"))
(def brady-bunch (into kids-of-mike kids-of-carol))
(println brady-bunch) ; -> (Cindy Jan Marcia Greg Peter Bobby)
```

`peek` 和 `pop` 可以用来把 list 当作一个堆栈来操作. 她们操作的都是 list 的第一个元素。

# 向量

Vectors 也是一种有序的集合。这种集合对于从最后面删除一个元素，或者获取最后面一个元素是非常高效的(O(1))。这意味着对于向 vector 里面添加元素使用 conj 被使用 cons 更高效。Vector 对于以索引的方式访问某个元素（用 nth 命令）或者修改某个元素(用 assoc)来说非常的高效。函数定义的时候指定参数列表用的就是 vector。

下面是两种创建 vector 的方法：

```
(def stooges (vector "Moe" "Larry" "Curly"))
(def stooges ["Moe" "Larry" "Curly"])
```

除非你要写的程序要特别用到 list 的从前面添加/删除效率很高的这个特性， 否则一般来说我们鼓励你们用 vector 而不是 lists。这主要是因为语法上 `[...]` 比 ‘ `(...)` 更自然，更不容易弄混淆。因为函数，宏以及 special form 的语法也是 `(...)。`

`get` 获取 vector 里面指定索引的元素. 我们后面会看到 get 也可以从 map 里面获取指定 key 的 value。索引是从 0 开始的。 `get` 函数和函数 `nth` 类似. 它们都接收一个可选的默认值参数 — 如果给定的索引超出边界，那么会返回这个默认值。如果没有指定默认值而索引又超出边界了， `get` 函数会返回 `nil` 而 `nth` 会抛出一个异常. 看例子:

```
(get stooges 1 "unknown") ; -> "Larry"
(get stooges 3 "unknown") ; -> "unknown"
```

`assoc` 可以对 vectors 和 maps 进行操作。 当用在 vector 上的时候, 它会从给定的 vector 创建一个新的 vector, 而指定的那个索引所对应的元素被替换掉。如果指定的这个索引等于 vector 里面元素的数目，那么我们会把这个元素加到新 vector 的最后面去；如果指定的索引比 vector 的大小要大，那么一个 `IndexOutOfBoundsException` 异常会被抛出来。看代码:

```
(assoc stooges 2 "Shemp") ; -> ["Moe" "Larry" "Shemp"]
```

`subvec` 获取一个给定 vector 的子 vector。它接受三个参数，一个 vectore, 一个起始索引以及一个可选的结束索引。如果结束索引没有指定，那么默认的结束索引就是 vector 的大小。新的 vector 和原来的 vector 共享内存(所以高效)。

所有上面的对于 list 的例子代码对于 vector 同样适用。 `peek` 和 `pop` 函数对于 vector 同样适用, 只是它们操作的是 vector 的最后一个元素，而对于 list 操作的则是第一个函数。 `conj` 函数从一个给定的 vector 创建一个新的 vector — 添加一个元素到新的 vector 的最后面去. `cons` 函数从一个给定的 vector 创建一个新的 vector — 添加一个新的元素到 vector 的最前面去。

# 集合

Sets 是一个包含不重复元素的集合。当我们要求集合里面的元素不可以重复，并且我们不要求集合里面的元素保持它们添加时候的顺序，那么 sets 是比较适合的。 Clojure 支持两种不同的 set： 排序的和不排序的。如果添加到 set 里面的元素相互之间不能比较大小，那么一个 `ClassCastException` 异常会被抛出来。下面是一些创建 set 的方法:

```clj
(def stooges (hash-set "Moe" "Larry" "Curly")) ; not sorted
(def stooges #{"Moe" "Larry" "Curly"}) ; same as previous
(def stooges (sorted-set "Moe" "Larry" "Curly"))
```

`contains?` 函数可以操作 sets 和 maps. 当操作 set 的时候, 它返回给定的 set 是否包含某个元素。这比在 list 和 vector 上面使用的 `some函数就简单多了` . 看例子:

```
(contains? stooges "Moe") ; -> true
(contains? stooges "Mark") ; -> false
```

Sets 可以被当作它里面的元素的函数来使用. 当以这种方式来用的时候，返回值要么是这个元素，要么是 nil. 这个比起 contains？函数来说更简洁. 比如:

```
(stooges "Moe") ; -> "Moe"
(stooges "Mark") ; -> nil
(println (if (stooges person) "stooge" "regular person"))
```

在介绍 list 的时候提到的函数 `conj` 和 `into` 对于 set 也同样适用. 只是元素的顺序只有对 sorted-set 才有定义.

`disj` 函数通过去掉给定的 set 里面的一些元素来创建一个新的 set. 看例子:

```
(def more-stooges (conj stooges "Shemp")) ; -> #{"Moe" "Larry" "Curly" "Shemp"}
(def less-stooges (disj more-stooges "Curly")) ; -> #{"Moe" "Larry" "Shemp"}
```

你也可以看看 `clojure.set` 名字空间里面的一些函数: `difference` , `index` , `intersection` , `join` , `map-invert` , `project` , `rename` , `rename-keys` , `select` 和 `union` . 其中有些函数的操作的对象是 map 而不是 set。

# 映射

Maps 保存从 key 到 value 的 a 对应关系 — key 和 value 都可以是任意对象。key-value 组合被以一种可以按照 key 的顺序高效获取的方式保存着。

下面是创建 map 的一些方法， 其中逗号是为了提高可读性的，它是可选的，解析的时候会被当作空格忽略掉的。

```
(def popsicle-map
  (hash-map :red :cherry, :green :apple, :purple :grape))
(def popsicle-map
  {:red :cherry, :green :apple, :purple :grape}) ; same as previous
(def popsicle-map
  (sorted-map :red :cherry, :green :apple, :purple :grape))
```

Map 可以作为它的 key 的函数，同时如果 key 是 keyword 的话，那么 key 也可以作为 map 的函数。下面是三种获取:green 所对应的值的方法：

```
(get popsicle-map :green)
(popsicle-map :green)
(:green popsicle-map)
```

`contains?` 方法可以操作 sets 和 maps. 当被用在 map 上的时候，它返回 map 是否包含给定的 key. `keys` 函数返回 map 里面的所有的 key 的集合. `vals` 函数返回 map 里面所有值的集合. 看例子:

```
(contains? popsicle-map :green) ; -> true
(keys popsicle-map) ; -> (:red :green :purple)
(vals popsicle-map) ; -> (:cherry :apple :grape)
```

`assoc` 函数可以操作 maps 和 vectors. 当被用在 map 上的时候，它会创建一个新的 map， 同时添加任意对新的 name-value pair, 如果某个给定的 key 已经存在了，那么它的值会被更新。看例子:

```
(assoc popsicle-map :green :lime :blue :blueberry)
; -> {:blue :blueberry, :green :lime, :purple :grape, :red :cherry}
```

`dissoc` 创建一个新的 map， 同时忽略掉给定的那么些 key， 看例子:

```
(dissoc popsicle-map :green :blue) ; -> {:purple :grape, :red :cherry}
```

我们也可以把 map 看成一个简单的集合，集合里面的每个元素是一个 pair: name-value: `clojure.lang.MapEntry` 对象. 这样就可以和 doseq 跟 destructuring 一起使用了, 它们的作用都是更简单地来遍历 map， 我们会在后面详细地介绍这些函数. 下面的这个例子会遍历 `popsicle-map` 里面的所有元素，把 key bind 到 `color，` 把 value bind 到 `flavor。` `name函数返回一个keyword的字符串名字。`

```
(doseq [[color flavor] popsicle-map]
  (println (str "The flavor of " (name color)
    " popsicles is " (name flavor) ".")))
```

上面的代码的输出是这样的：

```
The flavor of green popsicles is apple.
The flavor of purple popsicles is grape.
The flavor of red popsicles is cherry.
```

`select-keys` 函数接收一个 map 对象，以及一个 key 的集合的参数，它返回这个集合里面 key 在那个集合里面的一个子 map。看例子:

```
(select-keys popsicle-map [:red :green :blue]) ; -> {:green :apple, :red :cherry}
```

`conj` 函数添加一个 map 里面的所有元素到另外一个 map 里面去。如果目标 map 里面的 key 在源 map 里面也有，那么目标 map 的值会被更新成源 map 里面的值。

map 里面的值也可以是一个 map， 而且这样嵌套无限层。获取嵌套的值是非常简单的。同样的，更新一个嵌套的值也是很简单的。

为了证明这个， 我们会创建一个描述人（person）的 map。其中内嵌了一个表示人的地址的 map，同时还有一个叫做 employer 的内嵌 map。

```
(def person {
  :name "Mark Volkmann"
  :address {
    :street "644 Glen Summit"
    :city "St. Charles"
    :state "Missouri"
    :zip 63304}
  :employer {
    :name "Object Computing, Inc."
    :address {
      :street "12140 Woodcrest Executive Drive, Suite 250"
      :city "Creve Coeur"
      :state "Missouri"
      :zip 63141}}})
```

`get-in` 函数、宏 `->` 以及函数 `reduce` 都可以用来获得内嵌的 key. 下面展示了三种获取这个人的 employer 的 address 的 city 的值的方法：

```
(get-in person [:employer :address :city])
(-> person :employer :address :city) ; explained below
(reduce get person [:employer :address :city]) ; explained below
```

宏 `->` 我们也称为 “thread” 宏, 它本质上是调用一系列的函数，前一个函数的返回值作为后一个函数的参数. 比如下面两行代码的作用是一样的:

```
(f1 (f2 (f3 x)))
(-> x f3 f2 f1)
```

在名字空间 `clojure.contrib.core` 里面还有个 -?>宏， 它会马上返回 nil， 如果它的调用链上的任何一个函数返回 nil (short-circiut)。这会避免抛出 `NullPointerException` 异常。

`reduce` 函数接收一个需要两个参数的函数, 一个可选的 value 以及一个集合。它会以 value 以及集合的第一个元素作为参数来调用给定的函数（如果指定了 value 的话）， 要么以集合的第一个元素以及第二个元素为参数来调用给定的函数（如果没有指定 value 的话)。接着就以这个返回值以及集合里面的下一个元素为参数来调用给定的函数，知道集合里面的元素都被计算了 — 最后返回一个值. 这个函数与 ruby 里面的 `inject` 以及 Haskell 里面的 `foldl` 作用是一样的。

`assoc-in` 函数可以用来修改一个内嵌的 key 的值。看下面的例子把 person 的 employer->address->city 修改成 Clayton 了。

```
(assoc-in person [:employer :address :city] "Clayton")
```

`update-in` 函数也是用来更新给定的内嵌的 key 对应的值，只是这个新值是通过一个给定的函数来计算出来。下面的例子里面会把 person 的 employer->address->zip 改成旧的 zip + “-1234″。看例子:

```
(update-in person [:employer :address :zip] str "-1234") ; using the str function
```
