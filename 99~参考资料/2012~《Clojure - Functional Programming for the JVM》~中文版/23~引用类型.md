# 引用类型

引用类型是一种可变引用指向不可变数据的一种机制。Clojure 里面有 4 种引用类型:[Vars](#Vars),[Refs](#Refs),[Atoms](#Atoms) 和[Agents](#Agents). 它们有一些共同的特征:

- 它们都可以指向任意类型的对象。
- 都可以利用函数`deref` 以及宏`@` 来读取它所指向的对象。
- 它们都支持验证函数，这些函数在它们所指向的值发生变化的时候自动调用。如果新值是合法的值，那么验证函数简单的返回 true, 如果新值是不合法的，那么要么返回 false， 要么抛出一个异常。如果只是简单地返回了`false`, 那么一个`IllegalStateException` 异常会被抛出，并且带着提示信息： "Invalid reference state" 。
- 如果是 Agents 的话，它们还支持 watchers。如果被监听的引用的值发生了变化，那么 Agent 会得到通知， 详情见 "[Agents](#Agents)" 一节。

下面的这个表格总结了一下四种引用类型的区别，以及分别要用什么方法去创建或者修改它们。这个表格里面提到的函数我们会在后面介绍。

|          | Var                                                                                                                                                                                  | Ref                                                                                                                                                                                    | Atom                                                                                                                            | Agent                                                                                 |
| -------- | ------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------ | -------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------- | ------------------------------------------------------------------------------------------------------------------------------- | ------------------------------------------------------------------------------------- |
| 目的     | 同步对于一个线程本地(thread-local)的变量的修改。                                                                                                                                     | 同步、协调对于一个或者多个值的修改                                                                                                                                                     | 同步对于一个值的修改                                                                                                            | 对一个值进行异步修改                                                                  |
| 创建方法 | `(def *name* *initial-value*)`                                                                                                                                                       | `(ref *initial-value*)`                                                                                                                                                                | `(atom *initial-value*)`                                                                                                        | `(agent *initial-value*)`                                                             |
| 修改方法 | `(def *name* *new-value*)` 可以赋新的值 `(alter-var-root (var *name*) *update-fn* *args*)` 自动设置新值`(set! *name* *new-value*) 在一个binding form 里满设置一个新的、线程本地的值` | `(ref-set *ref* *new-value*)` `必须在dosync里面调用` `(alter *ref* *update-fn* *arguments*)` ` 必须在dosync里面调用``(commute *ref* *update-fn* *arguments*) ` 必须在`dosync 里面调用` | `(reset! *atom* *new-value*)` ` (compare-and-set! *atom* *current-value* *new-value*)``(swap! *atom* *update-fn* *arguments*) ` | `(send *agent* *update-fn* *arguments*)` `(send-off *agent* *update-fn* *arguments*)` |

### Vars

[Vars](http://clojure.org/vars) 是一种可以有一个被所有线程共享的 root binding 并且每个线程线程还能有自己线程本地(thread-local)的值的一种引用类型。

下面的语法创建一个 Var 并且给它一个 root binding:

```
(def name value)
```

你可以不给它一个值的。如果你没有给它一个值，那么我们说这个 Var 是 "unbound". 同样的语法可以用来修改一个 Var 的 root binding。

有两种方法可以创建一个已经存在的 Var 的线程本地 binding(thread-local-binding):

```
(binding [name expression] body)
(set! name expression) ; inside a binding that bound the same name
```

关于[binding](#bindings) 宏的用法我们前面已经介绍过了. 下面的例子演示把它和`set!` 一起使用. 用 set!来修改一个由 binding bind 的 Var 的线程本地的值。

```
(def v 1)

(defn change-it []
  (println "2) v =" v) ; -> 1

  (def v 2) ; changes root value
  (println "3) v =" v) ; -> 2

  (binding [v 3] ; binds a thread-local value
    (println "4) v =" v) ; -> 3

    (set! v 4) ; changes thread-local value
    (println "5) v =" v)) ; -> 4

  (println "6) v =" v)) ; thread-local value is gone now -> 2

(println "1) v =" v) ; -> 1

(let [thread (Thread. #(change-it))]
  (.start thread)
  (.join thread)) ; wait for thread to finish

(println "7) v =" v) ; -> 2
```

我们一般不鼓励使用 Vars， 因为线程之间对于同一个 Var 的修改没有做很好的协调，比如线程 A 在使用一个 Var 的 root 值，然后才发现，在它使用这个值的时候，已经有一个线程 B 在修改这个值了。

### Refs

Refs 是用来协调对于一个或者多个 binding 的并发修改的。这个协调机制是利用[Software Transactional Memory](http://en.wikipedia.org/wiki/Software_transactional_memory) (STM)来实现的。 Refs 指定在一个事务里面修改。

STM 在某些方面跟数据库的事务很像。在一个 STM 事务里面做的修改只有在事务提交之后别的线程才能看到。这实现了 ACID 里面的 A 和 I。Validation 函数是的对 Ref 的修改与跟它相关的其它的值是一致的(consistent), 也就实现了 C。

要想你的代码在一个事务里面执行， 那么要把你的代码包在宏`dosync` 的体内。当在一个事务里面对值进行修改，被改的其实是一个私有的、线程内的、直到事务提交才会被别的线程看到的一快内存。

如果到事务结束的时候也没有异常抛出的话， 那么这个事务会顺利的提交， 在事务里面所作的改变也就可以被别的线程看到了。

如果在事务里面有一个异常抛出，包括 validation 函数抛出的异常，那么这个事务会被回滚，事务里面对值做的修改也就会撤销。

如果在一个事务里面，我们要对一个 Ref 进行修改，但是发现从我们的事务开始之后，已经有别的线程对这个 Ref 做了改动（冲突了）， 那么当前事务里面的改动会被撤销，然后从 dosync 的开头重试。那到底什么时候会检测到冲突， 什么时候会进行重试， 这个是没有保证的， 唯一保证的是 clojure 为检测到冲突，并且会进行重试。

要在事务里面执行的代码一定要是没有副作用的，这一点非常重要，因为前面提到的，事务可能会跟别的事务事务冲突，然后重试， 如果有副作用的话，那么出来的结果就不对了。不过要执行有副作用的代码也是可能的， 可以把这个方法调用包装给 Agent, 然后这个方法会被 hold 住直到事务成功提交，然后执行一次。如果事务失败那么就不会执行。

`ref `函数可以创建一个 Ref 对象。下面的例子代码创建一个 Ref 并且得到它的引用。

```
(def name (ref value))
```

`dosync `宏用来包裹一个事务 -- 从它对应的左括号开始，到它对应的右括号结束。在事务里面我们用`ref-set` 来改变一个 Ref 的值并且返回这个值。你不能在事务之外调用这个函数，否则会抛出`IllegalStateException` 异常。 看例子：

```
(dosync
  ...
  (ref-set name new-value)
  ...)
```

如果你要赋的新值是基于旧的值的话，那么就需要三个步骤了：

1. deference 这个 Ref 来获得它的旧值
2. 计算新值
3. 设置新值

`alter` 和`commute` 函数在一个操作里面完成这三个步骤。 `alter` 函数是用来操作那些必须以特定顺序进行的修改。而`commute` 函数则是要来操作那些修改顺序不是很重要 -- 可以同时进行的修改。 跟`ref-set`, 一样， 它们只能在一个事务里面调用。它们都接受一个 "update 函数" 做为参数， 以及一些额外的参数来计算新的值。这个函数会被传递这个 Ref 在线程内的当前的值以及一些额外的参数（如果有的话）。当我们要赋的新的值是基于旧的值计算出来的时候, 那么我们鼓励使用`alter` 和`commute` 而不是`ref-set`.

比如，我们想给一个 Ref:`counter`加一， 我们可以用`inc` 函数来实现：

```
(dosync
  ...
  (alter counter inc)
  ; or as
  (commute counter inc)
  ...)
```

如果`alter` 试图修改的 Ref 在当前事务开始之后被别的事务改变了，那么当前事务会进行重试。而同样的情况下`commute` 不会进行重试。它会以事务内的当前值进行计算。这会获得比较好的性能(因为不进行重试)。但是要记住的是`commute` 函数只有在多个线程对 Ref 的修改顺序不重要的时候才能使用。

如果一个事务提交了， 那么对于`commute` 函数还会有一些额外的事情发生。对于每一个`commute` 调用, Ref 的值会被下面的调用结果重置:

```
(apply update-function last-committed-value-of-ref args)
```

注意，这个 update-function 会被传递这个 Ref 最后被提交的值， 这个值可能是另外一个、在我们当前事务开始之后才开始的事务。

使用`commute` 而不是`alter` 是一种优化。只要对 Ref 进行更新的顺序不会影响到这个 Ref 的最终的值。

然后看一个使用了 Refs 和 Atoms (后面会介绍)的例子。这个例子涉及到银行账户以及账户之间的交易。首先我们定义一下数据模型。

```
(ns com.ociweb.bank)

; Assume the only account data that can change is its balance.
(defstruct account-struct :id :owner :balance-ref)

; We need to be able to add and delete accounts to and from a map.
; We want it to be sorted so we can easily
; find the highest account number
; for the purpose of assigning the next one.
(def account-map-ref (ref (sorted-map)))
```

下面的函数建立一个新的帐户，并且把它存入帐户的 map, ? 然后返回它。

```
(defn open-account
  "creates a new account, stores it in the account map and returns it"
  [owner]
  (dosync ; required because a Ref is being changed
    (let [account-map @account-map-ref
          last-entry (last account-map)
          ; The id for the new account is one higher than the last one.
          id (if last-entry (inc (key last-entry)) 1)
          ; Create the new account with a zero starting balance.
          account (struct account-struct id owner (ref 0))]
      ; Add the new account to the map of accounts.
      (alter account-map-ref assoc id account)
      ; Return the account that was just created.
      account)))
```

下面的函数支持从一个账户里面存/取钱。

```
(defn deposit [account amount]
  "adds money to an account; can be a negative amount"
  (dosync ; required because a Ref is being changed
    (Thread/sleep 50) ; simulate a long-running operation
    (let [owner (account :owner)
          balance-ref (account :balance-ref)
          type (if (pos? amount) "deposit" "withdraw")
          direction (if (pos? amount) "to" "from")
          abs-amount (Math/abs amount)]
      (if (>= (+ @balance-ref amount) 0) ; sufficient balance?
        (do
          (alter balance-ref + amount)
          (println (str type "ing") abs-amount direction owner))
        (throw (IllegalArgumentException.
                 (str "insufficient balance for " owner
                      " to withdraw " abs-amount)))))))

(defn withdraw
  "removes money from an account"
  [account amount]
  ; A withdrawal is like a negative deposit.
  (deposit account (- amount)))
```

下面是函数支持把钱从一个账户转到另外一个账户。由`dosync` 所开始的事务保证转账要么成功要么失败，而不会出现中间状态。

```
(defn transfer [from-account to-account amount]
  (dosync
    (println "transferring" amount
             "from" (from-account :owner)
             "to" (to-account :owner))
    (withdraw from-account amount)
    (deposit to-account amount)))
```

下面的函数支持查询账户的状态。由`dosync` 所开始的事务保证事务之间的一致性。比如把不会报告一个转账了一半的金额。

```
(defn- report-1 ; a private function
  "prints information about a single account"
  [account]
  ; This assumes it is being called from within
  ; the transaction started in report.
  (let [balance-ref (account :balance-ref)]
    (println "balance for" (account :owner) "is" @balance-ref)))

(defn report
  "prints information about any number of accounts"
  [& accounts]
  (dosync (doseq [account accounts] (report-1 account))))
```

上面的代码没有去处理线程启动时候可能抛出的异常。相反，我们在当前线程给他们定义了一个异常处理器。

```
; Set a default uncaught exception handler
; to handle exceptions not caught in other threads.
(Thread/setDefaultUncaughtExceptionHandler
  (proxy [Thread$UncaughtExceptionHandler] []
    (uncaughtException [thread throwable]
      ; Just print the message in the exception.
      (println (.. throwable .getCause .getMessage)))))
```

现在我们可以调用上面的函数了。

```
(let [a1 (open-account "Mark")
      a2 (open-account "Tami")
      thread (Thread. #(transfer a1 a2 50))]
  (try
    (deposit a1 100)
    (deposit a2 200)

    ; There are sufficient funds in Mark's account at this point
    ; to transfer $50 to Tami's account.
    (.start thread) ; will sleep in deposit function twice!

    ; Unfortunately, due to the time it takes to complete the transfer
    ; (simulated with sleep calls), the next call will complete first.
    (withdraw a1 75)

    ; Now there are insufficient funds in Mark's account
    ; to complete the transfer.

    (.join thread) ; wait for thread to finish
    (report a1 a2)
    (catch IllegalArgumentException e
      (println (.getMessage e) "in main thread"))))
```

上面代码的输出是这样的：

```
depositing 100 to Mark
depositing 200 to Tami
transferring 50 from Mark to Tami
withdrawing 75 from Mark
transferring 50 from Mark to Tami (a retry)
insufficient balance for Mark to withdraw 50
balance for Mark is 25
balance for Tami is 200
```

### Validation 函数

在继续介绍下一个引用类型之前，下面是一个 validation 函数的例子，他验证所有赋给 Ref 的值是数字。

```
; Note the use of the :validator directive when creating the Ref
; to assign a validation function which is integer? in this case.
(def my-ref (ref 0 :validator integer?))

(try
  (dosync
    (ref-set my-ref 1) ; works

    ; The next line doesn't work, so the transaction is rolled back
    ; and the previous change isn't committed.
    (ref-set my-ref "foo"))
  (catch IllegalStateException e
    ; do nothing
    ))

(println "my-ref =" @my-ref) ; due to validation failure -> 0
```

### Atoms

Atoms 提供了一种比使用 Refs&STM 更简单的更新当个值的方法。它不受事务的影响

有三个函数可以修改一个 Atom 的值：`reset!`,`compare-and-set!` 和`swap!`.

`reset!` 函数接受两个参数：要设值的 Atom 以及新值。它设置新的值，而不管你旧的值是什么。看例子：

```
(def my-atom (atom 1))
(reset! my-atom 2)
(println @my-atom) ; -> 2
```

`compare-and-set!` 函数接受三个参数：要被修改的 Atom, 上次读取时候的值，新的值。 这个函数在设置新值之前会去读 Atom 现在的值。如果与上次读的时候的值相等， 那么设置新值并返回 true, 否则不设置新值， 返回 false。看例子：

```
(def my-atom (atom 1))

(defn update-atom []
  (let [curr-val @my-atom]
    (println "update-atom: curr-val =" curr-val) ; -> 1
    (Thread/sleep 50) ; give reset! time to run
    (println
      (compare-and-set! my-atom curr-val (inc curr-val))))) ; -> false

(let [thread (Thread. #(update-atom))]
  (.start thread)
  (Thread/sleep 25) ; give thread time to call update-atom
  (reset! my-atom 3) ; happens after update-atom binds curr-val
  (.join thread)) ; wait for thread to finish

(println @my-atom) ; -> 3
```

为什么最后的结果是 3 呢? `update-atom` 被放在一个单独的线程里面，在`reset!` 函数调用之前执行。所以它获取了 atom 的初始值 1(存到变量 curr-val 里面去了), 然后它 sleep 了以让`reset!` 函数有执行是时间。在那之后，atom 的值就变成 3 了。当`update-atom` 函数调用`compare-and-set!` 来给这个值加一的时候， 它发现 atom 的值已经不是它上次读取的那个值了(1), 所以更新失败， atom 的值还是 3。

`swap!` 函数接受一个要修改的 Atom, 一个计算 Atom 新值的函数以及一些额外的参数(如果需要的话)。这个计算 Atom 新的值的函数会以这个 Atom 以及一些额外的参数做为输入。swap！函数实际上是对 compare-and-set!函数的一个封装，但是有一个显著的不同。 它首先把 Atom 的当前值存入一个变量，然后调用计算新值的函数来计算新值， 然后再调用 compare-and-set!函数来赋值。如果赋值成功的话，那就结束了。如果赋值不成功的话， 那么它会重复这个过程，一直到赋值成功为止。这就是它们的区别：所以上面的代码可以用 swap!改写成这样：

```
(def my-atom (atom 1))

(defn update-atom [curr-val]
  (println "update-atom: curr-val =" curr-val)
  (Thread/sleep 50) ; give reset! time to run
  (inc curr-val))

(let [thread (Thread. #(swap! my-atom update-atom))]
  (.start thread)
  (Thread/sleep 25) ; give swap! time to call update-atom
  (reset! my-atom 3)
  (.join thread)) ; wait for thread to finish

(println @my-atom) ; -> 4
```

为什么输出变成 4 了呢？因为 swap!会不停的去给 curr-val 加一直到成功为止。

### Agents

Agents 是用把一些事情放到另外一个线程来做 -- 一般来说不需要事务控制的。它们对于修改一个单个对象的值(也就是 Agent 的值)来说很方便。这个值是通过在另外的一个 thread 上面运行一个“action”来修改的。一个 action 是一个函数， 这个函数接受 Agent 的当前值以及一些其它参数。 **Only one action at a time will be run on a given Agent 在任意一个时间点一个 Agent 实例上面只能运行一个 action.**

`agent` 函数可以建立一个新的 Agent. 比如:

```
(def my-agent (agent initial-value))
```

`send` 函数把一个 action 分配给一个 Agent， 并且马上返回而不做任何等待。 这个 action 会在另外一个线程(一般是由一个线程池提供的)上面单独运行。 当这个 action 运行结束之后，返回值会被设置给这个 Agent。`send-off` 函数也类似只是线程来自另外一个线程吃。`send` 使用一个 "固定大小的" 线程吃 (java.util.concurrent.Executors 里面的[newFixedThreadPool](<http://java.sun.com/javase/6/docs/api/java/util/concurrent/Executors.html#newFixedThreadPool(int)>) ) ， 线程的个数是机器的处理器的个数加 2。如果所有的线程都被占用，那么你如果要运行新的 action， 那你就要等了。`send-off` 使用的是 "cached thread pool" (java.util.concurrent.Executors 里面的?[newCachedThreadPool](<http://java.sun.com/javase/6/docs/api/java/util/concurrent/Executors.html#newCachedThreadPool()>)) ， 这个线程池里面的线程的个数是按照需要来分配的。

如果`send` 或者`send-off` 函数是在一个事务里面被调用的。 那么这个 action 直到线程提交的时候才会被发送给另外一个线程去执行。这在某种程度上来说和`commute` 函数对 Ref 的作用是类似的。

在 action 里面， 相关联的那个 agent 可以通过 symbol：`*agent*`得到。

`await` 以一个或者多个 Agent 作为参数， 并且 block 住当前的线程，直到当前线程分派给这些 Agent 的 action 都执行完了。`await-for` 函数是类似的, 但是它接受一个超时时间作为它的第一个参数， 如果在超时之前事情都做完了， 那么返回一个非 nil 的值， 否则返回一个非 nil 的值，而且当前线程也就不再被 block 了。`await` 和`await-for` 函数不能在事务里面调用。

如果一个 action 执行的时候抛出一个异常了，那么你要 dereference 这个 Agent 的话也会抛出异常的。在 action 里面抛出的所有的异常可以通过`agent-errors` 函数获取。 `clear-agent-errors` 函数可以清除一个指定 Agent 上面的所有异常。

`shutdown-agents` 函数等待所有发送给 agents 的 action 都执行完毕。然后它停止线程池里面所有的线程。在这之后你就不能发送新的 action 了。我们一定要调用`shutdown-agents` 以让 JVM 可以正常退出，因为 Agent 使用的这些线程不是守护线程， 如果你不显式关闭的话，JVM 是不会退出的。

### Watchers

WARNING: 下面这个章节要做一些更新，因为在 Clojure1.1 里面`add-watcher` 和`remove-watcher` 这两个函数被去掉了。 两个不大一样的函数`add-watch` 和`remove-watch` 被添加进来了。

Agents 可以用作其它几种引用类型的监视器。当一个被监视的引用的值发生了改变之后，Clojure 会通过给 Agent 发送一个 action 的形式通知它。通知的类型可以是`send` 或者`send-off`, 这个是在你把 Agent 注册为引用类型的监视器的时候指定的。那个 action 的参数是那个监视器 Agent 以及发生改变的引用对象。这个 action 的返回值则是 Agent 的新值。

就像我们前面已经说过的那样，函数式编程强调那种“纯函数” -- 不会改变什么全局变量的函数。但是 Clojure 也不绝对静止这样做， 但是 Clojure 使得我们要找出对全局状态进行了改变的函数非常的简单。一个方法就是寻找那些能对状态进行改变的宏和方法，比如`alter`。 这到了调用这些宏/函数的地方就找到了所有修改全局状态的地方了。另外一个方法就是用 Agent 来监视对于全局状态的更改。一个监视者可以通过 dump 出来 stack trace 来确定到底是谁对全局状态做了修改。

下面的例子给一个 Var，一个 Ref, 一个 Atom 注册了一个 Agent 监视者。Agent 里面维护了它所监视的每个引用被修改的次数(一个 map)。这个 map 的 key 就是引用对象，而值则是被修改的次数。

```
(def my-watcher (agent {}))

(defn my-watcher-action [current-value reference]
  (let [change-count-map current-value
        old-count (change-count-map reference)
        new-count (if old-count (inc old-count) 1)]
  ; Return an updated map of change counts
  ; that will become the new value of the Agent.
  (assoc change-count-map reference new-count)))

(def my-var "v1")
(def my-ref (ref "r1"))
(def my-atom (atom "a1"))

(add-watcher (var my-var) :send-off my-watcher my-watcher-action)
(add-watcher my-ref :send-off my-watcher my-watcher-action)
(add-watcher my-atom :send-off my-watcher my-watcher-action)

; Change the root binding of the Var in two ways.
(def my-var "v2")
(alter-var-root (var my-var) (fn [curr-val] "v3"))

; Change the Ref in two ways.
(dosync
  ; The next line only changes the in-transaction value
  ; so the watcher isn't notified.
  (ref-set my-ref "r2")
  ; When the transaction commits, the watcher is
  ; notified of one change this Ref ... the last one.
  (ref-set my-ref "r3"))
(dosync
  (alter my-ref (fn [_] "r4"))) ; And now one more.

; Change the Atom in two ways.
(reset! my-atom "a2")
(compare-and-set! my-atom @my-atom "a3")

; Wait for all the actions sent to the watcher Agent to complete.
(await my-watcher)

; Output the number of changes to
; each reference object that was watched.
(let [change-count-map @my-watcher]
  (println "my-var changes =" (change-count-map (var my-var))) ; -> 2
  (println "my-ref changes =" (change-count-map my-ref)) ; -> 2
  (println "my-atom changes =" (change-count-map my-atom))) ; -> 2

(shutdown-agents)
```
