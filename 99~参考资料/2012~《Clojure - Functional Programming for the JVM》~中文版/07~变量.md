# 变量

Clojure 里面是不支持变量的。它跟变量有点像，但是在被赋值之前是不允许改的，包括：全局 binding, 线程本地(thread local)binding， 以及函数内的本地 binding， 以及一个表达式内部的 binding。

`def` 这个 special form 定义一个全局的 binding，并且你还可以给它一个”root value” ，这个 root value 在所有的线程里面都是可见的，除非你给它赋了一个线程本地的值. `def` 也可以用来改变一个已经存在的 binding 的 root value —— 但是这是不被鼓励的，因为这会牺牲不可变数据所带来的好处。

函数的参数是只在这个函数内可见的本地 binding。

`let` 这个 special form 创建局限于一个 当前 form 的 bindings. 它的第一个参数是一个 vector, 里面包含名字-表达式的对子。表达式的值会被解析然后赋给左边的名字。这些 binding 可以在这个 vector 后面的表达式里面使用。这些 binding 还可以被多次赋值以改变它们的值，let 命令剩下的参数是一些利用这个 binding 来进行计算的一些表达式。注意：如果这些表达式里面有调用别的函数，那么这个函数是无法利用 let 创建的这个 binding 的。

宏 `binding` 跟 `let 类似` , 但是它创建的本地 binding 会暂时地覆盖已经存在的全局 binding. 这个 binding 可以在创建这个 binding 的 form 以及这个 form 里面调用的函数里面都能看到。但是一旦跳出了这个 `binding` 那么被覆盖的全局 binding 的值会回复到之前的状态。

从 Clojure 1.3 开始, binding 只能用在 动态变量(dynamic var)上面了. 下面的例子演示了怎么定一个 dynamic var。另一个区别是 `let` 是串行的赋值的, 所以后面的 binding 可以用前面 binding 的值, 而 `binding` 是不行的.

要被用来定义成新的、本地线程的、用 binding 来定义的 binding 有它们自己的命名方式：她们以星号开始，以星号结束。在这篇文章里面你会看到： `*command-line-args*` , `*agent*` , `*err*` , `*flush-on-newline*` , `*in*` , `*load-tests*` , `*ns*` , `*out*` , `*print-length*` , `*print-level*` and `*stack-trace-depth*` .要使用这些 binding 的函数会被这些 binding 的值影响的。比如给*out*一个新的 binding 会改变 println 函数的输出终端。

下面的例子介绍了 `def` , `let` 和 `binding` 的用法。

```
(def ^:dynamic v 1) ; v is a global binding

(defn f1 []
  (println "f1: v =" v)) ; global binding

(defn f2 []
  (println "f2: before let v =" v) ; global binding
  (let [v 2] ; creates local binding v that shadows global one
    (println "f2: in let, v =" v) ; local binding
    (f1))
  (println "f2: after let v =" v)) ; global binding

(defn f3 []
  (println "f3: before binding v =" v) ; global binding
  (binding [v 3] ; same global binding with new, temporary value
    (println "f3: in binding, v =" v) ; global binding
    (f1))
  (println "f3: after binding v =" v)) ; global binding

(defn f4 []
 (def v 4)) ; changes the value of the global binding

(f2)
(f3)
(f4)
(println "after calling f4, v =" v)
```

上面代码的输出是这样的：

```
f2: before let v = 1
f2: in let, v = 2
f1: v = 1 (let DID NOT change value of global binding)
f2: after let v = 1
f3: before binding v = 1
f3: in binding, v = 3
f1: v = 3 (binding DID change value of global binding)
f3: after binding v = 1 (value of global binding reverted back)
after calling f4, v = 4
```
