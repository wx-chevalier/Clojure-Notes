# 迭代

有很多方法可以遍历一个集合。

宏`dotimes` 会执行给定的表达式一定次数, 一个本地 binding 会被给定值：从 0 到一个给定的数值. 如果这个本地 binding 是不需要的 (下面例子里面的`card-number` ), 可以用下划线来代替， 看例子:

```
(dotimes [card-number 3]
  (println "deal card number" (inc card-number))) ; adds one to card-number
```

注意下上面例子里面的`inc` 函数是为了让输出变成 1, 2, 3 而不是 0, 1, 2。上面代码的输出是这样的:

```
deal card number 1
deal card number 2
deal card number 3
```

宏`while` 会一直执行一个表达式只要指定的条件为 true. 下面例子里面的`while` 会一直执行，只要这个线程没有停:

```
(defn my-fn [ms]
  (println "entered my-fn")
  (Thread/sleep ms)
  (println "leaving my-fn"))

(let [thread (Thread. #(my-fn 1))]
  (.start thread)
  (println "started thread")
  (while (.isAlive thread)
    (print ".")
    (flush))
  (println "thread stopped"))
```

上面代码的输出是这样的:

```
started thread
.....entered my-fn.
.............leaving my-fn.
thread stopped
```

### List Comprehension

宏`for` 和`doseq` 可以用来做 list comprehension. 它们支持遍历多个集合 (最右边的最快) ，同时还可以做一些过滤用`:when 和` `:while。` 宏`for` 只接受一个表达式 , 它返回一个懒惰集合作为结果. 宏`doseq` 接受任意数量的表达式, 以有副作用的方式执行它们, 并且返回`nil`.

下面的例子会打印一个矩阵里面 所有的元素出来。 它们会跳过 "B" 列 并且只输出小于 3 的那些行。我们会在“[序列](#sequences)”那一节介绍`dorun` , 它会强制提取 for 所返回的懒惰集合.

```clj
(def cols "ABCD")
(def rows (range 1 4)) ; purposely larger than needed to demonstrate :while

(println "for demo")
(dorun
  (for [col cols :when (not= col \B)
        row rows :while (< row 3)]
    (println (str col row))))

(println "\ndoseq demo")
(doseq [col cols :when (not= col \B)
        row rows :while (< row 3)]
  (println (str col row)))
```

上面的代码的输出是这样的：

```
for demo
A1
A2
C1
C2
D1
D2

doseq demo
A1
A2
C1
C2
D1
D2
```

宏`loop` 是一个 special form, 从它的名字你就可以猜出来它是用来遍历的. 它以及和它类似的`recur` 会在下一节介绍.
